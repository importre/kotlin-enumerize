apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

afterEvaluate { project ->
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    publishing {
        publications {
            LibPublish(MavenPublication) {
                from components.java
                groupId GROUP
                artifactId project.name
                version VERSION_NAME
                artifact sourcesJar
                artifact javadocJar
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')

        publications = ['LibPublish']
        publish = true
        dryRun = false

        pkg {
            repo = 'maven'
            name = project.name
            vcsUrl = VCS_URL
            licenses = ['MIT']
            publicDownloadNumbers = true
            version {
                name = VERSION_NAME
                vcsTag = VERSION_NAME
                gpg {
                    sign = true
                    passphrase = System.getenv('BINTRAY_PASSPHRASE')
                }
            }
        }
    }
}
